plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def marvelApiKeysPropertiesFile = rootProject.file("marvel_api_keys.properties")
def marvelApiKeysProperties = new Properties()
marvelApiKeysProperties.load(new FileInputStream(marvelApiKeysPropertiesFile))

android {
    compileSdk 31

    signingConfigs {
        production {
            storePassword keystoreProperties['storePassword']
            keyPassword keystoreProperties['keyPassword']
            keyAlias keystoreProperties['keyAlias']
            storeFile file(keystoreProperties['storeFile'])
        }
    }

    defaultConfig {
        applicationId "fr.emiliennury.cleancoding"
        minSdk 21
        targetSdk 31

        def major = Integer.parseInt(property("version.major"))
        def minor = Integer.parseInt(property("version.minor"))
        def patch = Integer.parseInt(property("version.patch"))
        def buildNumber = Integer.parseInt(property("version.buildNumber"))

        versionCode buildNumber
        versionName "${major}.${minor}.${patch}-b${buildNumber}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "MARVEL_PRIVATE_API_KEY", "\"${marvelApiKeysProperties['privateKey']}\""
        buildConfigField "String", "MARVEL_PUBLIC_API_KEY", "\"${marvelApiKeysProperties['publicKey']}\""
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.production
        }
    }

    flavorDimensions "environnement"

    productFlavors {

        development {
            dimension "environnement"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            buildConfigField "String", "BASE_URL", "\"https://gateway.marvel.com:443/\""
        }

        production {
            dimension "environnement"
            applicationIdSuffix ".prod"
            versionNameSuffix "-prod"
            buildConfigField "String", "BASE_URL", "\"https://gateway.marvel.com:443/\""
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "io.insert-koin:koin-android:3.1.6"
    implementation("androidx.navigation:navigation-fragment-ktx:2.4.2")
    implementation("androidx.navigation:navigation-ui-ktx:2.4.2")
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation 'com.google.code.gson:gson:2.8.8'

    testImplementation 'junit:junit:4.+'
    testImplementation "io.insert-koin:koin-test:3.1.6"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}